let mod: const i32 = 998244353;
let d: i32;

fn multiply(a: i32, b: i32) -> i32 {
    if (b == 0) return 0;
    if (b == 1) return a % mod;
    let cur: i32 = multiply(a, b/2);
    cur = (cur + cur) % mod;
    if (b % 2 == 1) return (cur + a) % mod;
    else return cur;
}

fn power(a: i32, b: i32) -> i32 {
    if (b == 0) return 1;
    let cur: i32 = power(a, b/2);
    cur = multiply(cur, cur);
    if (b % 2 == 1) return multiply(cur, a);
    else return cur;
}
let maxlen: const i32 = 2097152;
let temp: [i32; maxlen];
let a: [i32; maxlen];
let b: [i32; maxlen];
let c: [i32; maxlen];

fn MemMove(dst: *mut i32, dst_pos: i32, src: *mut i32, len: i32) -> i32 {
    let i: i32 = 0;
    while (i < len){
        dst[dst_pos + i] = src[i];
        i = i + 1;
    }
    return i;
}

fn fft(arr: *mut i32, begin_pos: i32, n: i32, w: i32) -> i32 {
    if (n == 1) return 1;
    let i: i32 = 0;
    while (i < n){
        if (i % 2 == 0) temp[i / 2] = arr[i + begin_pos];
        else temp[n / 2 + i / 2] = arr[i + begin_pos];
        i = i + 1;
    }

    MemMove(arr, begin_pos, temp, n);
    fft(arr, begin_pos, n / 2, multiply(w, w));
    fft(arr, begin_pos + n / 2, n / 2, multiply(w, w));
    i = 0;
    let wn: i32 = 1;
    while (i < n / 2){
        let x: i32 = arr[begin_pos + i];
        let y: i32 = arr[begin_pos + i + n / 2];
        arr[begin_pos + i] = (x + multiply(wn, y)) % mod;
        arr[begin_pos + i + n / 2] = (x - multiply(wn, y) + mod) % mod;
        wn = multiply(wn, w);
        i = i + 1;
    }
    return 0;
}

fn main() -> i32 {
    let n: i32 = getarray(a);
    let m: i32 = getarray(b);
    starttime();
    d = 1;
    while (d < n + m - 1){
        d = d * 2;
    }
    fft(a, 0, d, power(3, (mod - 1) / d));
    fft(b, 0, d, power(3, (mod - 1) / d));

    let i: i32 = 0;
    while (i < d){
        a[i] = multiply(a[i], b[i]);
        i = i + 1;
    }
    fft(a, 0, d, power(3, mod-1 - (mod-1)/d));
    i = 0;
    while (i < d){
        a[i] = multiply(a[i], power(d, mod-2));
        i = i + 1;
    }
    stoptime();
    putarray(n + m - 1, a);
    return 0;
}
